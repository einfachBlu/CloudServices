plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'de.blu'
version '1.1.0-SNAPSHOT'

shadowJar {
    relocate 'io.netty', 'shadow.io.netty'
    relocate 'io.lettuce', 'shadow.io.lettuce'
}

dependencies {
    implementation project(':common')

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation 'com.google.inject:guice:4.0'
    implementation 'io.lettuce:lettuce-core:5.2.2.RELEASE'

    compileOnly 'net.md-5:bungeecord-api:1.16-R0.4-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'

    compileOnly 'commons-io:commons-io:2.5'
}

shadowJar.doLast {
    File jarFile = file("${project.buildDir}/libs/${project.name}-${project.version}-all.jar")

    if (!jarFile.exists()) {
        jarFile = file("${project.buildDir}/libs/${project.name}-${project.version}.jar")
        if (!jarFile.exists()) {
            //println "output jarFile doesnt exist, maybe something failed with the build?"
            return
        }
    }

    copy {
        from jarFile
        into "${project.rootDir}/output/"

        rename {
            "server-connector.jar"
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
publishing {
    /*
    repositories {
        maven {
            credentials {
                username "${miniMavenUser}"
                password "${miniMavenPassword}"
            }
            url = 'http://0.0.0.0:8081/repository/custom/'
        }
    }
    */
    publications {
        myrepository(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}